set -l progname pacrs

function listall
    if which paru &> /dev/null
        paru -Pc | string replace ' ' \t
    else
        __fish_print_pacman_packages
    end
end

set -l listgroups "(pacman -Sg)\t'Package Group'"
set -l listinstalled "(pacman -Q | string replace ' ' \t)"

set -l install '__fish_seen_subcommand_from in install'
set -l remove '__fish_seen_subcommand_from rm remove'
set -l packages '__fish_seen_subcommand_from pa packages'
set -l search '__fish_seen_subcommand_from se search'
set -l mark '__fish_seen_subcommand_from m mark'
set -l info '__fish_seen_subcommand_from i info'
set -l cache '__fish_seen_subcommand_from ca cache'

complete -c $progname -e # Erase old completion
complete -c $progname -f # Disable file completions for entire command

complete -c $progname -a 'install' -d 'Install packages'
complete -c $progname -a 'remove' -d 'Remove packages and unneeded dependencies'
complete -c $progname -a 'upgrade' -d 'Upgrade the system'
complete -c $progname -a 'packages' -d 'Print list of installed packages'
complete -c $progname -a 'info' -d 'Print info about package'
complete -c $progname -a 'search' -d 'Search packages'
complete -c $progname -a 'cache' -d 'Cache'
complete -c $progname -a 'mark' -d 'Mark packages'
complete -c $progname -a 'help' -d 'Print this message or the help of the given subcommand(s)'

complete -c $progname -n $install -s o -l orphaned -d 'Remove orphaned packages'
complete -c $progname -n $packages -s u -l updated -d 'Print list of packages that were updated in the repo'
complete -c $progname -n $packages -s o -l orphaned -d 'Print list of orphaned packages'
complete -c $progname -n $packages -s a -l aur -d 'Print list of AUR packages'
complete -c $progname -n $mark -s e -l explicit -d 'Mark packages as installed explicit'
complete -c $progname -n $cache -s s -l size -d 'Print occupied size on disk by cache'
complete -c $progname -n $cache -s c -l clean -d 'Clean packages from cache and unused repos'
complete -c $progname -n $cache -s u -l uninstalled -d 'Clean only cache of uninstalled packages'
complete -c $progname -n $mark -s d -l dependencie -d 'Mark packages as dependencie'
complete -c $progname -s h -l help -d 'Print help message'

complete -c $progname -n $install -xa "(listall) $listgroups"
complete -c $progname -n $remove -xa $listinstalled
complete -c $progname -n $search -xa "(listall) $listgroups"
complete -c $progname -n $info -xa "(listall) $listgroups"
