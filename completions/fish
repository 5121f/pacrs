# SPDX-License-Identifier: GPL-3.0-only

set -l progname pacrs

function listall
    if which paru &>/dev/null
        paru -Pc | string replace ' ' \t
    else
        __fish_print_pacman_packages
    end
end

set -l listgroups "(pacman -Sg)\t'Package Group'"
set -l listinstalled "(pacman -Q | string replace ' ' \t)"

set -l install '__fish_seen_subcommand_from in install'
set -l remove '__fish_seen_subcommand_from rm remove'
set -l autoremove '__fish_seen_subcommand_from ar autoremove'
set -l update '__fish_seen_subcommand_from up update'
set -l packages '__fish_seen_subcommand_from pa packages'
set -l search '__fish_seen_subcommand_from se search'
set -l info '__fish_seen_subcommand_from if info'
set -l listupdates '__fish_seen_subcommand_from lu listupdates'
set -l files '__fish_seen_subcommand_from fl files'
set -l clean '__fish_seen_subcommand_from cc clean'
set -l mark '__fish_seen_subcommand_from mr mark'
set -l ps '__fish_seen_subcommand_from ps'

complete -c $progname -e # Erase old completion
complete -c $progname -f # Disable file completions for entire command

# Primary operations
complete -c $progname -a install -d 'Install packages'
complete -c $progname -a remove -d 'Remove (uninstall) packages'
complete -c $progname -a autoremove -d 'Remove unneeded packages'
complete -c $progname -a update -d 'Update installed packages with newer versions'
complete -c $progname -a packages -d 'Display list of all available packages'
complete -c $progname -a info -d 'Display detailed information about the specified packages'
complete -c $progname -a search -d 'Search for packages matching any of the given search strings'
complete -c $progname -a list-updates -d 'List available updates'
complete -c $progname -a files -d 'Query the file database'
complete -c $progname -a clean -d 'Clean pacman cache. By default cleans the entire cache'
complete -c $progname -a mark -d 'Mark packages'
complete -c $progname -a ps -d 'Display processes which use deleted files and needed to restart'
complete -c $progname -a help -d 'Print this message or the help of the given subcommand(s)'

# General options
complete -c $progname -s h -l help -d 'Print help'
complete -c $progname -s V -l version -d 'Print version'

# Install options
complete -c $progname -n $install -s o -l orphaned -d 'Remove orphaned packages'

# Remove options
complete -c $progname -n $remove -s u -l clean-deps -d 'Automatically remove dependencies which become unneeded after removal of requested packages'

# Packages options
complete -c $progname -n $packages -s s -l search -d 'Search for given string in installed packages'
complete -c $progname -n $packages -s e -l explicit -d 'Display list of explicit installed packages'
complete -c $progname -n $packages -s d -l deps -d 'Display list of packages installed as dependency'
complete -c $progname -n $packages -s o -l orphaned -d 'Print list of orphaned packages'
complete -c $progname -n $packages -s a -l aur -d 'Display list of packages which not found in databases. In most cases it\'s AUR packages'

# Info options
complete -c $progname -n $info -s r -l recursive-deps -d 'Display list of recursive dependencies'

# List updates
complete -c $progname -n $listupdates -s V -l verbose -d 'Verbose output'

# Files options
complete -c $progname -n $files -xa "(listall)"
complete -c $progname -n $files -s f -l find -d 'Find specific file among all packages'
complete -c $progname -n $files -s U -l not-update-index -d 'Don\'t update files index'

# Clean options
complete -c $progname -n $clean -s u -l uninstalled -d 'Clean cache of uninstalled packages'
complete -c $progname -n $clean -s a -l aur -d 'Clean AUR cache'
complete -c $progname -n $clean -s k -l keep -d 'Keep "num" of each package in the cache'
complete -c $progname -n $clean -s s -l show_remove_candidates -d 'Only show remove candidates without actually removing'

# Mark options
complete -c $progname -n $mark -s e -l explicit -d 'Mark packages as installed explicit'
complete -c $progname -n $mark -s d -l dependency -d 'Mark packages as dependency'

# ps options
complete -c $progname -n $ps -s s -l sort-by -d 'Sort output by given field'
complete -c $progname -n $ps -s o -l shorter -d 'Show only list of commands instead of table'
complete -c $progname -n $ps -s r -l reverse -d 'Reverse sorting'

for condition in $remove $autoremove
    complete -c $progname -n $condition -xa $listinstalled
end

for condition in $install $search $info
    complete -c $progname -n $condition -xa "(listall) $listgroups"
end

for confition in $update $files $ps
    complete -c $progname -n $confition -s q -l quiet -d 'Don\'t show additional messages'
end
