set -l progname pacrs

set -l listall "(paru -Pc | string replace ' ' \t)"
set -l listgroups "(pacman -Sg)\t'Package Group'"
set -l listinstalled "(pacman -Q | string replace ' ' \t)"

set -l install '__fish_seen_subcommand_from in install'
set -l remove '__fish_seen_subcommand_from rm remove'
set -l list '__fish_seen_subcommand_from l list'
set -l search '__fish_seen_subcommand_from se search'
set -l mark '__fish_seen_subcommand_from m mark'
set -l info '__fish_seen_subcommand_from i info'

complete -c $progname -e # Erase old completion
complete -c $progname -f # Disable file completions for entire command

complete -c $progname -a 'install' -d 'Install packages'
complete -c $progname -a 'remove' -d 'Remove packages and unneeded dependencies'
complete -c $progname -a 'upgrade' -d 'Upgrade the system'
complete -c $progname -a 'list' -d 'Print list of installed packages'
complete -c $progname -a 'info' -d 'Print info about package'
complete -c $progname -a 'search' -d 'Search packages'
complete -c $progname -a 'mark' -d 'Mark packages'
complete -c $progname -a 'help' -d 'Print this message or the help of the given subcommand(s)'

complete -c $progname -n $install -s o -l orphaned -d 'Remove orphaned packages'
complete -c $progname -n $list -s u -l upgradable -d 'Print list of packages that were updated in the repo'
complete -c $progname -n $list -s o -l orphaned -d 'Print list of orphaned packages'
complete -c $progname -n $mark -s e -l explicit -d 'Mark packages as installed explicit'
complete -c $progname -n $mark -s d -l dependencie -d 'Mark packages as dependencie'
complete -c $progname -s h -l help -d 'Print help message'

complete -c $progname -n $install -xa "$listall $listgroups"
complete -c $progname -n $remove -xa $listinstalled
complete -c $progname -n $search -xa "$listall $listgroups"
complete -c $progname -n $info -xa "$listall $listgroups"
